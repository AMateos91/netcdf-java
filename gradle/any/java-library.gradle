apply plugin: 'java-library'
apply from: "$rootDir/gradle/any/javadoc.gradle"
apply from: "$rootDir/gradle/any/testing.gradle"
apply from: "$rootDir/gradle/any/archiving.gradle"
apply from: "$rootDir/gradle/any/publishing.gradle"
apply from: "$rootDir/gradle/any/spotless.gradle"

apply plugin: 'maven-publish'

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

def sourceJar = tasks.register('sourceJar', Jar) {
  from sourceSets.main.allJava
}

def javadocJar = tasks.register('javadocJar', Jar) {
  dependsOn javadoc
  from files(javadoc.destinationDir)
}

publish.configure {
  dependsOn sourceJar
  dependsOn javadocJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact('sourceJar') {
        classifier 'sources'
      }
      artifact('javadocJar') {
        classifier 'javadoc'
      }
      from components.java
      pom {
        //name = name
        description = title
        scm {
          connection = 'scm:git:https://github.com/Unidata/netcdf-java.git'
          developerConnection = 'scm:git:https://github.com/Unidata/netcdf-java.git'
          url = 'https://www.unidata.ucar.edu/software/netcdf-java/'
        }
      }
    }
  }
}

// Will apply to "compileJava", "compileTestJava", "compileSourceSetJava", etc.
tasks.withType(JavaCompile).all {
  options.encoding = 'UTF-8'

  // show deprecation details
  options.compilerArgs = ['-Xlint:deprecation']
}
